$GUI Plot Link

  $ECHO NONE
  $IO OUTPUT GEOMETRY\BALL\COMMERCIAL\AWIBASELINE.CSV +FILE(10)
  $END
  $SCR 1
\
  $SCR 1
added reflecting surface on bottom of channel only \
  $SCR 1
EMISSION (um), EXCITATION (um),N_LENS, LENS_DIA (um), EFF (um), BFL (um), PDMS_CHAN_LENS (um), PDMS_LENS_DET (um), WD_PDMS, COAT_REFL , AIR_GAP (um), EX_ANGLE, EX_XSHIFT, DIFF_MMF, DET_RAD (um), BASE_RAYS, N_RAYS, BASE_THETA, N_THETA, BASE_EX, N_EX

  $IO OUTPUT CLOSE
  $ECHO


LOOP_1 {  !! nested macros
                        
$ITER PDMS_LENS_DET 0 300 -4 NUMBEROFRAYS 
  {  
  &REG PDMS_LENS_DET

  CONSIDER ALL
  SYSTEM NEW
  RESET   
  
  !! Optical variables
  FRESNEL AVE
  FLUX_TOTAL=1000
  EM_WAVE=0.670       !! emission wavelength
  EX_WAVE=0.625       !! emission wavelength
  SPOT_SIZE=10
      
  !! Define units and wavelength
  UNITS MICRONS 'Watts" 
  WAVELENGTHS (EM_WAVE) UM

  !! Define media
  !!N_LENS=1.768
  N_EFF=(N_LENS)/1.43
  N_CORE=1.46
  N_CLAD=1.44
  MEDIA ; 1.43     'PDMS'
  MEDIA ; 1.51     'GLASS'
  MEDIA ; 1.33     'WATER'
  MEDIA ; (N_LENS) 'BALL'
  MEDIA ; (N_CLAD) 'DOPED 
  MEDIA ; (N_CORE) 'SILICA'
                      
  !! Including 
  SPLIT 1500 MONTECARLO       !! monte carlo simulation
  LEVEL 1000 OFF 
  FRESNEL AVE                 !! fresnel equation

  !! Coating properties
  COAT_REFL=1 !!R^2
  COAT_TRANS=1-(COAT_REFL)^2
  COATING PROPERTIES 0; 0 1 'TRANSMIT' 
  COATING PROPERTIES 0; 0 0 'ABSORB' 
  COATING PROPERTIES 0; 1 0 'REFLECT' 
  COATING PROPERTIES 0; (COAT_REFL) (COAT_TRANS) 'REFL_COAT' 
    
  !! Simulation Variables
  
  !! Chip variables
  CHIP_LENGTH=5000            !! Length and width of microchip
  PDMS_THICK_BOTTOM=300       !! Total thickness of PDMS adjacent/below the channels  
  GLASS_THICK=200             !! Total thickness of glass layer
  CHAN_WIDTH=25
  
  !! Lens dimensions
  !!LENS_DIA=150                 !! Diamter of the lens
  LENS_RAD=(LENS_DIA)/2  
  
      TERM_A=(N_EFF)-1
      TERM_B=2/(LENS_RAD)
      TERM_C=((TERM_A)*(LENS_DIA))/((N_EFF)*(LENS_RAD)*(LENS_RAD))
      LENS_P=(TERM_A)*((TERM_B)-(TERM_C))

  EFL=1/(LENS_P)
  BFL=(EFL)-(LENS_RAD)
  
  PDMS_CHAN_LENS=(BFL)  !! Thickness of PDMS layer above the channel
  !!PDMS_LENS_DET=170
  
  AIR_GAP=2                   !! air gap beneath detector
  !!DET_RAD=50
  WD_PDMS=(PDMS_CHAN_LENS)+(LENS_DIA)+(PDMS_LENS_DET)
   
  !! Bounding Surface 
  SURFACE 
  PLANE Z (WD_PDMS) 0 0 ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'BoundingPlane' 
  !!PLOT SURFACE 

  !! Beginning of geometry modeling 
  ENT OBJ 
  
  !! Channel 
  PLANE Z 0 RECTANGLE (CHIP_LENGTH)/2.2 (CHAN_WIDTH)/2 'CHANNEL.TOP' 
  INTERFACE COATING BARE WATER PDMS 
  
  PLANE Z -(CHAN_WIDTH) RECTANGLE (CHIP_LENGTH)/2.2 (CHAN_WIDTH)/2 'CHANNEL.B0TTOM' 
  INTERFACE COATING REFL_COAT WATER PDMS 
  
  PLANE Y 0 RECTANGLE (CHAN_WIDTH)/2 (CHIP_LENGTH)/2.2 'CHANNEL.BACK' 
  INTERFACE COATING BARE WATER PDMS 
  SHIFT Z -(CHAN_WIDTH)/2 
  SHIFT Y (CHAN_WIDTH)/2 
  
  PLANE Y 0 RECTANGLE (CHAN_WIDTH)/2 (CHIP_LENGTH)/2.2 'CHANNEL.FRONT' 
  INTERFACE COATING BARE WATER PDMS 
  SHIFT Z -(CHAN_WIDTH)/2 
  SHIFT Y -(CHAN_WIDTH)/2 
  
  PLANE X 0 RECTANGLE (CHAN_WIDTH)/2 (CHAN_WIDTH)/2 'CHANNEL.RIGHT' 
  INTERFACE COATING BARE WATER PDMS 
  SHIFT X (CHIP_LENGTH)/2.2 
  SHIFT Z -(CHAN_WIDTH)/2 
  
  PLANE X 0 RECTANGLE (CHAN_WIDTH)/2 (CHAN_WIDTH)/2 'CHANNEL.LEFT' 
  INTERFACE COATING BARE WATER PDMS 
  SHIFT X -(CHIP_LENGTH)/2.2 
 
  !! Glass-layer (substrate)
  TUBE Z -(PDMS_THICK_BOTTOM)  (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 -(PDMS_THICK_BOTTOM)-(GLASS_THICK) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 0 0 'GLASS.SIDE' 
  INTERFACE COATING BARE GLASS AIR 
          
  PLANE Z -(PDMS_THICK_BOTTOM)-(GLASS_THICK) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'GLASS.BOTTOM' 
  INTERFACE COATING BARE GLASS AIR  
  
  !! PDMS-layer
  TUBE Z (WD_PDMS) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 -(PDMS_THICK_BOTTOM) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 0 0 'MANTEL.SIDE' 
  INTERFACE COATING BARE PDMS AIR 
  
  PLANE Z -(PDMS_THICK_BOTTOM) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'MANTEL.BOTTOM' 
  INTERFACE COATING BARE PDMS GLASS 
  
  !! Air Gap
  PLANE Z (WD_PDMS) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'AIR_GAP' 
  INTERFACE COATING BARE PDMS AIR 
  
  !! Detector 
  PLANE Z (WD_PDMS)+(AIR_GAP) ELLIPSE (DET_RAD) (DET_RAD) 'DETECTOR' 
  INTERFACE COATING ABSORB DOPED AIR     
  
!!!!!! Multimode fiber
  CORE=125                    !! core diameter
  CLADDING=250                !! cladding diameter
  F2POSZ=(WD_PDMS)            !! Z-position of MMF
  F2POSX=(DET_RAD)+(CLADDING)/2+(DIFF_MMF) !! X-Shift of multimode fiber
  ANGLE=ATAN[(F2POSX)/((F2POSZ)+((CHAN_WIDTH)/2))]  !!43
  A2=0                           !! Rotation Angle 
  RATIO=1                     !! taper ratio
  CORETAPER=(CORE)/RATIO      !! diameter of tapered end
  CLADDINGTAPER=(CLADDING)/RATIO 
  F2LENGTH=850                !!taper length
  FIBER=5000                  !! length of remaining fiber
  CF=0.8862269  !! Conversion factor 1/e^2 to ASAP's Gaussian definition    

!! Multimode fiber 
  ENT OBJ 
    
  !! Core 
  TUBE Z (F2POSZ) (CORETAPER)/2 (CORETAPER)/2 (F2POSZ)+(FIBER) (CORE)/2 (CORE)/2 0 0 'MMF.CORE' 
    SHIFT X (F2POSX) 
    ROTATE Y (ANGLE) (F2POSZ) (F2POSX)-(CLADDINGTAPER)/2
    INTERFACE COATING BARE SILICA DOPED  
    BOUNDS +1 
  
  !! Cladding 
  TUBE Z (F2POSZ) (CLADDINGTAPER)/2 (CLADDINGTAPER)/2 (F2POSZ)+(F2LENGTH) (CLADDING)/2 (CLADDING)/2 0 0 'MMF.CLADDING' 
    SHIFT X (F2POSX) 
    ROTATE Y (ANGLE) (F2POSZ) (F2POSX)-(CLADDINGTAPER)/2
    INTERFACE COATING ABSORB AIR DOPED  
    BOUNDS +1 
  
  !! Core bottom 
  PLANE Z (F2POSZ) ELLIPSE (CLADDING) (CLADDING) 'MMF.CORE.BOTTOM' 
    SHIFT X (F2POSX)
    INTERFACE COATING BARE PDMS SILICA  
    BOUNDS -.3 
  
  !! Cladding bottom 
  PLANE Z (F2POSZ) ELLIPSE (CLADDING) (CLADDING) 'MMF.CLADDING.BOTTOM' 
    SHIFT X (F2POSX)                                                     
    INTERFACE COATING BARE PDMS DOPED 
    BOUNDS +.4 -.3 
  
  !! Remaining fiber end 
  PLANE Z (F2POSZ)+(F2LENGTH)+(FIBER) ELLIPSE (CLADDING)/2 (CLADDING)/2 (CORE)/(CLADDING) 'FIBER.CLADDING.TOP' 
    SHIFT X (F2POSX) 
    ROTATE Y (ANGLE) (F2POSZ) (F2POSX)-(CLADDINGTAPER)/2                                                         
    INTERFACE COATING ABSORB DOPED AIR 
  
  TUBE Z (F2POSZ)+(F2LENGTH) (CORE)/2 (CORE)/2 (F2POSZ)+(F2LENGTH)+(FIBER) (CORE)/2 (CORE)/2 0 0 'FIBER.CORE' 
    SHIFT X (F2POSX) 
    ROTATE Y (ANGLE) (F2POSZ) (F2POSX)-(CLADDINGTAPER)/2                                                        
    INTERFACE COATING BARE SILICA DOPED 
  
  TUBE Z (F2POSZ)+(F2LENGTH) (CLADDING)/2 (CLADDING)/2 (F2POSZ)+(F2LENGTH)+(FIBER) (CLADDING)/2 (CLADDING)/2 0 0 'FIBER.CLADDING' 
    SHIFT X (F2POSX) 
    ROTATE Y (ANGLE) (F2POSZ) (F2POSX)-(CLADDINGTAPER)/2 
    INTERFACE COATING ABSORB AIR DOPED                                                                                                                                 
                                                                                                                                
  !!!!!!!!!!!!!!!!!!!
  RAYS 0 
  IMMERSE WATER             !! light source is inside the water 
      
  EMITTING SPHEROID 0 0 -(CHAN_WIDTH)/2 (SPOT_SIZE) (SPOT_SIZE) (SPOT_SIZE) (FLUX_TOTAL) 'FLUOROPHORE' ISO 
  FLUX TOTAL (FLUX_TOTAL) 
  WAVELENGTHS (EM_WAVE) UM
  HALT 10000 
  
    CONSIDER ALL 
    WINDOW Z X 
    TRACE PLOT OVERLAY
    PLOT FACETS
    CONSIDER ONLY DETECTOR 
    STATS ALL           
    $GRAB 'TOTAL' 0 1 BASENRAYS

  BASETHETA=ACOS[1-(2*(BASENRAYS)/(FLUX_TOTAL))]
  
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  
    RAYS 0
    IMMERSE SILICA
    PARABASALS 4
    BEAMS COHERENT DIFFRACT
    WAVELENGTHS (EX_WAVE) UM
    WIDTHS 1.6
    GAUSSIAN Z -(F2POSZ)-(F2LENGTH) (CHAN_WIDTH)/2 (CHAN_WIDTH)/2 100 100 0 0 (SPOT_SIZE)*(CF)/2 (SPOT_SIZE)*(CF)/2
      SOURCE DIRECTION 0 0 1
      SHIFT X (F2POSX) 
      ROTATE Y (ANGLE) ((F2POSZ)+((CHAN_WIDTH)/2)) (F2POSX)-(CLADDINGTAPER)/2
      ROTATE Y 180 0 0

    WINDOW Z X
    PIXELS 51 
    TRACE PLOT OVERLAY
    PLOT FACETS
    CONSIDER ONLY DETECTOR    
    STATS ALL      
    $GRAB 'TOTAL' 0 1 BASEEX                         
  
  !!!!!!!!!!!!!!!!!!!
  
  !! Lens
  ENT OBJ
  ELLIPSOID 3@(LENS_RAD) 0 0 (PDMS_CHAN_LENS)+(LENS_RAD) 'BALL_LENS'
    INTERFACE COATING TRANSMIT PDMS BALL
    REDEFINE COLOR 1
    
  !! Laser-induced fluorescence imitating light source with 100000 rays 
  RAYS 0 
  IMMERSE WATER             !! light source is inside the water 
  
  EMITTING SPHEROID 0 0 -(CHAN_WIDTH)/2 (SPOT_SIZE) (SPOT_SIZE) (SPOT_SIZE) (FLUX_TOTAL) 'FLUOROPHORE' ISO 
  FLUX TOTAL (FLUX_TOTAL) 
  WAVELENGTHS (EM_WAVE)*1000 NANOMETERS 
  HALT 10000 
  
    CONSIDER ALL 
    WINDOW Z X
    TRACE PLOT OVERLAY
    PLOT FACETS
    CONSIDER ONLY DETECTOR 
    STATS ALL
    $GRAB 'TOTAL' 0 1 NRAYS
    
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    
    RAYS 0
    IMMERSE SILICA
    PARABASALS 4
    BEAMS COHERENT DIFFRACT
    WAVELENGTHS (EX_WAVE) UM
    WIDTHS 1.6
    GAUSSIAN Z -(F2POSZ)-(F2LENGTH) (CHAN_WIDTH)/2 (CHAN_WIDTH)/2 100 100 0 0 (SPOT_SIZE)*(CF)/2 (SPOT_SIZE)*(CF)/2
      SOURCE DIRECTION 0 0 1
      SHIFT X (F2POSX) 
      ROTATE Y (ANGLE) ((F2POSZ)+((CHAN_WIDTH)/2)) (F2POSX)-(CLADDINGTAPER)/2
      ROTATE Y 180 0 0

    WINDOW Z X
    PIXELS 51 
    TRACE PLOT OVERLAY
    PLOT FACETS   
    $VIEW 
    CONSIDER ONLY DETECTOR    
    STATS ALL      
    $GRAB 'TOTAL' 0 1 NEX       
      
  !!!!!!!!!!!!!!!!!!!

  NTHETA=ACOS[1-(2*(NRAYS)/(FLUX_TOTAL))]
  
  
!!WRITES TO TEXT FILE ON EACH PASS
  $ECHO NONE
  $IO OUTPUT GEOMETRY\BALL\COMMERCIAL\AWIEXCITE.CSV +FILE(10)   !! ALL SUCCESSIVE ITERATIONS
  $END
  $SCR 1
\EM_WAVE.\, \EX_WAVE.\, \N_LENS.\ , \LENS_DIA.\, \EFL.\ , \BFL.\ ,\PDMS_CHAN_LENS.\ , \PDMS_LENS_DET.\ , \WD_PDMS.\, \COAT_REFL.\, \AIR_GAP.\, \ANGLE.\, \F2POSX.\, \DIFF_MMF.\, \DET_RAD.\, \BASERAYS.\ , \NRAYS.\, \BASETHETA.\ , \NTHETA.\, \BASEEX.\ , \NEX.\
  $IO OUTPUT CLOSE
  $ECHO
 
  }                         !!end of iteration loop

}

LOOP_2 {
  $ITER DET_RAD 20 200 -5  
  {
    &REG DET_RAD
  $ECHO NONE
  $IO OUTPUT GEOMETRY\BALL\COMMERCIAL\AWIEXCITE.CSV +FILE(10)   !! ALL SUCCESSIVE ITERATIONS
  $END
  $SCR 1
, , \N_LENS.\ , \LENS_DIA.\,  , , , , , , , , , , \DET_RAD.\ 
  $IO OUTPUT CLOSE
  $ECHO  

    $LOOP_1 ?
     
  }     
}

LOOP_3 {
  $ITER DIFF_MMF 0 200 -3
  {
    &REG DIFF_MMF
   
$ECHO NONE
$IO OUTPUT GEOMETRY\BALL\COMMERCIAL\AWIEXCITE.CSV +FILE(10)   !! ALL SUCCESSIVE ITERATIONS
$END
$SCR 1
, , \N_LENS.\ , \LENS_DIA.\
$IO OUTPUT CLOSE
$ECHO 
  
      $LOOP_2 ?  
  }     
}

LOOP_4 {
  $ITER LENS_DIA 150 250 -2  
  {
    &REG LENS_DIA
   
$ECHO NONE
$IO OUTPUT GEOMETRY\BALL\COMMERCIAL\AWIEXCITE.CSV +FILE(10)   !! ALL SUCCESSIVE ITERATIONS
$END
$SCR 1
, , \N_LENS.\ , \LENS_DIA.\ 
$IO OUTPUT CLOSE
$ECHO 
  
      $LOOP_3 ?  
  }     
}

  $ITER N_LENS 1.768 2.1 -2  
  {
    &REG N_LENS
$ECHO NONE
$IO OUTPUT GEOMETRY\BALL\COMMERCIAL\AWIEXCITE.CSV +FILE(10)   !! ALL SUCCESSIVE ITERATIONS
$END
$SCR 1
, , \N_LENS.\ 
$IO OUTPUT CLOSE
$ECHO 
  
    $LOOP_4 ?
  }


                            
RETURN

