  $ECHO NONE
  $IO OUTPUT GEOMETRY\BALL\PAPERS\YANG.CSV +FILE(10)
  $END
  $SCR 1
cubic zirconia lens half submerged in channel medium above channel, bare channel; analyzing dr vs wd vs rays
  $SCR 1
EMISSION (um), N_LENS, COAT_REFL, LENS_DIA (um), EFF (um), BFL (um), CHAN WIDTH (um), WELL HEIGHT (um), WELL WIDTH (um), PDMS_LENS_DET (um), AIR_GAP (um), WD_PDMS (um), DET_RAD (um), N_RAYS, N_RAYS_AREA

  $IO OUTPUT CLOSE
  $ECHO


LOOP_1  {  !! nested macros
                        
$ITER DET_RAD 12.5 (12.5*9) -9 NUMBEROFRAYS 
  {  
  &REG DET_RAD
  CONSIDER ALL
  SYSTEM NEW
  RESET   
  
  !! Optical variables
  FRESNEL AVE
  FLUX_TOTAL=10000
  EM_WAVE=0.590       !! emission wavelength
  SPOT_SIZE=10
      
  !! Define units and wavelength
  UNITS MICRONS 'Watts" 
  WAVELENGTHS (EM_WAVE)*1000 NANOMETERS 

  !! Define media
  N_LENS=2.1
  MEDIA ; 1.43     'PDMS' 
  MEDIA ; 1.44 	   'DOPED'
  MEDIA ; 1.51     'GLASS'
  MEDIA ; 1.33     'WATER'
  MEDIA ; (N_LENS) 'BALL'
  N_EFF=(N_LENS)/1.43
                      
  !! Including 
  SPLIT 1500 MONTECARLO       !! monte carlo simulation
  LEVEL 1000 OFF 
  FRESNEL AVE                 !! fresnel equation

  !! Coating properties
  COAT_REFL=-1!!R^2
  COAT_TRANS=1-(COAT_REFL)^2
  COATING PROPERTIES 0; 0 1 'TRANSMIT' 
  COATING PROPERTIES 0; 0 0 'ABSORB' 
  COATING PROPERTIES 0; 1 0 'REFLECT' 
  COATING PROPERTIES 0; (COAT_REFL) (COAT_TRANS) 'REFL_COAT' 
    
  !! Simulation Variables
  
  !! Chip variables
  CHIP_LENGTH=2000            !! Length and width of microchip
  PDMS_THICK_BOTTOM=300       !! Total thickness of PDMS adjacent/below the channels  
  GLASS_THICK=200             !! Total thickness of glass layer
  CHAN_WIDTH=25
  
  !! Lens dimensions
  LENS_DIA=(CHAN_WIDTH)       !! Diameter of the lens
  LENS_RAD=(LENS_DIA)/2  
  
      TERM_A=(N_EFF)-1
      TERM_B=2/(LENS_RAD)
      TERM_C=((TERM_A)*(LENS_DIA))/((N_EFF)*(LENS_RAD)*(LENS_RAD))
      LENS_P=(TERM_A)*((TERM_B)-(TERM_C))

  EFL=1/(LENS_P)
  BFL=(EFL)-(LENS_RAD)  
  
  !! microwell variables
  !!PDMS_LENS_DET=20           !! Thickness of PDMS layer above the lens
  WELL_WIDTH=(LENS_DIA)
  WELL_HEIGHT=(LENS_RAD)
  
  WD_PDMS=(LENS_RAD)+(WELL_HEIGHT)+(PDMS_LENS_DET)  !!PDMS WORKING DISTANCE
  AIR_GAP=2                                         !! air gap beneath detector
  !!DET_RAD=10
   
  !! Bounding Surface 
  SURFACE 
  PLANE Z (WD_PDMS)+(AIR_GAP) 0 0 ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'BoundingPlane' 
  !!PLOT SURFACE 

  !! Beginning of geometry modelling 
  ENT OBJ 
  
  !! Well
  
  PLANE Y 0 RECTANGLE (WELL_HEIGHT)/2 (WELL_WIDTH)/2 'WELL.BACK' 
  INTERFACE COATING BARE WATER PDMS 
  SHIFT Z (WELL_HEIGHT)/2
  SHIFT Y (WELL_WIDTH)/2
  
  PLANE Y 0 RECTANGLE (WELL_HEIGHT)/2 (WELL_WIDTH)/2 'WELL.FRONT' 
  INTERFACE COATING BARE WATER PDMS 
  SHIFT Z (WELL_HEIGHT)/2  
  SHIFT Y -(WELL_WIDTH)/2

  PLANE X 0 RECTANGLE (WELL_WIDTH)/2 (WELL_HEIGHT)/2 'WELL.LEFT' 
  INTERFACE COATING BARE WATER PDMS              
  SHIFT X -(WELL_WIDTH)/2 
  SHIFT Z (WELL_HEIGHT)/2
  
  PLANE X 0 RECTANGLE (WELL_WIDTH)/2 (WELL_HEIGHT)/2 'WELL.RIGHT' 
  INTERFACE COATING BARE WATER PDMS              
  SHIFT X (WELL_WIDTH)/2 
  SHIFT Z (WELL_HEIGHT)/2
  
  !! Channel   
  PLANE Z 0 RECTANGLE (((CHIP_LENGTH)/2.2)-(WELL_WIDTH))/2 (CHAN_WIDTH)/2 'CHANNEL.TOP.LEFT' 
  INTERFACE COATING BARE WATER PDMS              
  SHIFT X -(((CHIP_LENGTH)/4.4)-(WELL_WIDTH))  
  
  PLANE Z 0 RECTANGLE (((CHIP_LENGTH)/2.2)-(WELL_WIDTH))/2 (CHAN_WIDTH)/2 'CHANNEL.TOP.RIGHT' 
  INTERFACE COATING BARE WATER PDMS              
  SHIFT X ((CHIP_LENGTH)/4.4)-(WELL_WIDTH) 
  
  PLANE Z -(CHAN_WIDTH) RECTANGLE (CHIP_LENGTH)/2.2 (CHAN_WIDTH)/2 'CHANNEL.BOTTOM' 
  INTERFACE COATING BARE WATER PDMS 
  
  PLANE Y 0 RECTANGLE (CHAN_WIDTH)/2 (CHIP_LENGTH)/2.2 'CHANNEL.BACK' 
  INTERFACE COATING BARE WATER PDMS 
  SHIFT Z -(CHAN_WIDTH)/2 
  SHIFT Y (CHAN_WIDTH)/2 
  
  PLANE Y 0 RECTANGLE (CHAN_WIDTH)/2 (CHIP_LENGTH)/2.2 'CHANNEL.FRONT' 
  INTERFACE COATING BARE WATER PDMS 
  SHIFT Z -(CHAN_WIDTH)/2 
  SHIFT Y -(CHAN_WIDTH)/2 
  
  PLANE X 0 RECTANGLE (CHAN_WIDTH)/2 (CHAN_WIDTH)/2 'CHANNEL.RIGHT' 
  INTERFACE COATING BARE WATER PDMS 
  SHIFT X (CHIP_LENGTH)/2.2 
  SHIFT Z -(CHAN_WIDTH)/2 
  
  PLANE X 0 RECTANGLE (CHAN_WIDTH)/2 (CHAN_WIDTH)/2 'CHANNEL.LEFT' 
  INTERFACE COATING BARE WATER PDMS 
  SHIFT X -(CHIP_LENGTH)/2.2 
  SHIFT Z -(CHAN_WIDTH)/2  
 
  !! Glass-layer (substrate)
  TUBE Z -(PDMS_THICK_BOTTOM)  (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 -(PDMS_THICK_BOTTOM)-(GLASS_THICK) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 0 0 'GLASS.SIDE' 
  INTERFACE COATING BARE GLASS AIR 
          
  PLANE Z -(PDMS_THICK_BOTTOM)-(GLASS_THICK) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'GLASS.BOTTOM' 
  INTERFACE COATING BARE GLASS AIR  
  
  !! PDMS-layer
  TUBE Z (WD_PDMS) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 -(PDMS_THICK_BOTTOM) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 0 0 'MANTEL.SIDE' 
  INTERFACE COATING BARE PDMS AIR 
  
  PLANE Z -(PDMS_THICK_BOTTOM) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'MANTEL.BOTTOM' 
  INTERFACE COATING BARE PDMS GLASS 
  
  !! Air Gap
  PLANE Z (WD_PDMS) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'AIR_GAP' 
  INTERFACE COATING BARE PDMS AIR 
  
  !! Detector 
  PLANE Z (WD_PDMS)+(AIR_GAP) ELLIPSE (DET_RAD) (DET_RAD) 'DETECTOR' 
  INTERFACE COATING ABSORB DOPED AIR     
  
  !! Lens
  ENT OBJ
  
    OPTICAL Z (LENS_DIA) -(LENS_RAD) ELLIPSE (LENS_RAD) 'BALL.TOP'  
    INTERFACE COATING TRANSMIT BALL PDMS 
       
    OPTICAL Z 0 (LENS_RAD) ELLIPSE (LENS_RAD) 'BALL.BOTTOM'  
    INTERFACE COATING TRANSMIT BALL WATER
    
  !! Laser-induced fluorescence imitating light source with 100000 rays 
  RAYS 0 
  IMMERSE WATER             !! light source is inside the water 
  
  EMITTING SPHEROID 0 0 -(CHAN_WIDTH)/2 (SPOT_SIZE) (SPOT_SIZE) (SPOT_SIZE) (FLUX_TOTAL) 'FLUOROPHORE' ISO 
  FLUX TOTAL (FLUX_TOTAL) 
  WAVELENGTHS (EM_WAVE)*1000 NANOMETERS 
  HALT 10000 
  
    CONSIDER ALL 
    WINDOW Z X 
    TRACE PLOT OVERLAY
    PLOT FACETS 
    !!$VIEW 
    CONSIDER ONLY DETECTOR 
    STATS ALL        
    
    $GRAB 'TOTAL' 0 1 NUMBEROFRAYS 
    !!$GRAB 'TOTAL' 0 2 NFLUX
    &REG 

  RAYS_AREA=(NUMBEROFRAYS)/(3.14159*(DET_RAD)^2)
  
  !!WRITES TO TEXT FILE ON EACH PASS
  $ECHO NONE
  $IO OUTPUT GEOMETRY\BALL\PAPERS\YANG.CSV +FILE(10)   !! ALL SUCCESSIVE ITERATIONS
  $END
  $SCR 1
\EM_WAVE.\, \N_LENS.\ ,  \COAT_REFL.\ , \LENS_DIA.\, \EFL.\, \BFL.\ ,\CHAN_WIDTH.\, \WELL_HEIGHT.\ ,\WELL_WIDTH.\ , \PDMS_LENS_DET.\, \AIR_GAP.\, \WD_PDMS.\, \DET_RAD.\, \NUMBEROFRAYS.\, \RAYS_AREA.\

  $IO OUTPUT CLOSE
  $ECHO
  
  }   
  
}
  

!!LOOP_2  {
    $ITER PDMS_LENS_DET 0 150 -4  
    {
      &REG PDMS_LENS_DET
      $LOOP_1 ?
    }
!!}


!!LOOP_3  {
!!    $ITER CHAN_WIDTH 25 50 -2  
!!    {
!!      &REG CHAN_WIDTH
!!      $LOOP_2 ?
!!    }
!!}

!!$ITER R 0.5 0.9 -2
!!{  
!!  &REG R  
!!  $LOOP_3 ?
!!}

                            
RETURN

