CONSIDER ALL 

SYSTEM NEW 
RESET 

!! Define units and wavelength
UNITS MICRONS 'Watts" 
WAVELENGTHS 488 NANOMETERS 

!! Define media
MEDIA ; 1.43 'PDMS' 
MEDIA ; 1.30 'GLAS' 
MEDIA ; 1.33 'WATER' 
MEDIA ; 1.46 'SILICA' 
MEDIA ; 1.44 'DOPED' 

!! Including 
SPLIT 1500 MONTECARLO       !! monte carlo simulation
LEVEL 1000 OFF 
FRESNEL AVE                 !! fresnel equation

!! Simulation Variables
THICK=40                    !! Thickness of PDMS layer above the channels
LENGTH1=2000                !! Length and width of microchip
ML=300                      !! Total thickness of PDMS layer

!! Multimode fiber
ANGLE=43
F2POSZ=(THICK)+0            !! Z-position of MMF
F2POSX=230                  !! X-Shift of multimode fiber 
A2=0                        !! Rotation Angle
CORE=400                    !! core diameter
CLADDING=440                !! cladding diameter 
RATIO=1                     !! taper ratio
CORETAPER=(CORE)/RATIO      !! diameter of tapered end
CLADDINGTAPER=(CLADDING)/RATIO 
F2LENGTH=850                !!taper length
FIBER=5000                  !! length of remaining fiber

!! Coating properties
COATING PROPERTIES 0; 0 1 'TRANSMIT' 
COATING PROPERTIES 0; 0 0 'ABSORB' 
COATING PROPERTIES 0; 1 0 'REFLECT' 

!! Bounding Surface 
SURFACE 
PLANE Z (THICK) 0 0 ELLIPSE (LENGTH1)/2 (LENGTH1)/2 'BoundingPlane' 
PLOT SURFACE 

!! Beginning of geometry modeling 
ENT OBJ 

!! Channel 
PLANE Z 0 RECTANGLE (LENGTH1)/2.2 12.5 'CHANNEL.TOP' 
INTERFACE COATING BARE WATER PDMS 

PLANE Z -25 RECTANGLE (LENGTH1)/2.2 12.5 'CHANNEL.BUTTOM' 
INTERFACE COATING BARE WATER PDMS 

PLANE Y 0 RECTANGLE 12.5 (LENGTH1)/2.2 'CHANNEL.SIDE1' 
INTERFACE COATING BARE WATER PDMS 
SHIFT Z -12.5 
SHIFT Y 12.5 

PLANE Y 0 RECTANGLE 12.5 (LENGTH1)/2.2 'CHANNEL.SIDE2' 
INTERFACE COATING BARE WATER PDMS 
SHIFT Z -12.5 
SHIFT Y -12.5 

PLANE X 0 RECTANGLE 12.5 12.5 'CHANNEL.END1' 
INTERFACE COATING BARE WATER PDMS 
SHIFT X (LENGTH1)/2.2 
SHIFT Z -12.5 

PLANE X 0 RECTANGLE 12.5 12.5 'CHANNEL.END2' 
INTERFACE COATING BARE WATER PDMS 
SHIFT X -(LENGTH1)/2.2 
SHIFT Z -12.5 

!! PDMS-layer 
TUBE Z (THICK) (LENGTH1)/2 (LENGTH1)/2 -(ML) (LENGTH1)/2 (LENGTH1)/2 0 0 'MANTEL' 
INTERFACE COATING BARE PDMS AIR 

PLANE Z -(ML) ELLIPSE (LENGTH1)/2 (LENGTH1)/2 'MANTEL.BUTTOM' 
INTERFACE COATING BARE PDMS GLAS 

!! Glass-layer 
TUBE Z -(ML) (LENGTH1)/2 (LENGTH1)/2 -(ML)-200 (LENGTH1)/2 (LENGTH1)/2 0 0 'GLAS.MANTEL' 
INTERFACE COATING BARE GLAS AIR 

PLANE Z -(ML)-200 ELLIPSE (LENGTH1)/2 (LENGTH1)/2 'GLAS.BUTTOM' 
INTERFACE COATING BARE GLAS AIR 

!! Multimode fiber 

!! Core 
TUBE Z (F2POSZ) (CORETAPER)/2 (CORETAPER)/2 (F2POSZ)+(F2LENGTH) (CORE)/2 (CORE)/2 0 0 'MULTIMODEFIBER.CORE' 
INTERFACE COATING BARE SILICA DOPED 
SHIFT X (F2POSX) 
ROTATE Y (ANGLE) (F2POSZ) (F2POSX)-(CLADDINGTAPER)/2 
BOUNDS +1 

!! Cladding 
TUBE Z (F2POSZ) (CLADDINGTAPER)/2 (CLADDINGTAPER)/2 (F2POSZ)+(F2LENGTH) (CLADDING)/2 (CLADDING)/2 0 0 'MULTIMODEFIBER.CLADDING' 
INTERFACE COATING ABSORB AIR DOPED 
SHIFT X (F2POSX) 
ROTATE Y (ANGLE) (F2POSZ) (F2POSX)-(CLADDINGTAPER)/2 
BOUNDS +1 

!! Core bottom 
PLANE Z (F2POSZ) ELLIPSE (CLADDING) (CLADDING) 'MULTIMODEFIBER.CORE.BUTTOM' 
INTERFACE COATING BARE PDMS SILICA 
SHIFT X (F2POSX) 
BOUNDS -.3 

!! Cladding bottom 
PLANE Z (F2POSZ) ELLIPSE (CLADDING) (CLADDING) 'MULTIMODEFIBER.CLADDING.BUTTOM' 
INTERFACE COATING BARE PDMS DOPED 
SHIFT X (F2POSX) 
BOUNDS +.4 -.3 

!! PDMS-Layer on top 
PLANE Z (THICK) 0 0 ELLIPSE (LENGTH1)/2 (LENGTH1)/2 'PDMS.TOP' 
INTERFACE COATING BARE PDMS AIR 
BOUNDS +.4 

!! Remaining fiber end 
PLANE Z (F2POSZ)+(F2LENGTH)+(FIBER) ELLIPSE (CLADDING)/2 (CLADDING)/2 (CORE)/(CLADDING) 'FIBER.CLADDING.TOP' 
INTERFACE COATING ABSORB DOPED AIR 
SHIFT X (F2POSX) 
ROTATE Y (ANGLE) (F2POSZ) (F2POSX)-(CLADDINGTAPER)/2 

TUBE Z (F2POSZ)+(F2LENGTH) (CORE)/2 (CORE)/2 (F2POSZ)+(F2LENGTH)+(FIBER) (CORE)/2 (CORE)/2 0 0 'FIBER.CORE' 
INTERFACE COATING BARE SILICA DOPED 
SHIFT X (F2POSX) 
ROTATE Y (ANGLE) (F2POSZ) (F2POSX)-(CLADDINGTAPER)/2 

TUBE Z (F2POSZ)+(F2LENGTH) (CLADDING)/2 (CLADDING)/2 (F2POSZ)+(F2LENGTH)+(FIBER) (CLADDING)/2 (CLADDING)/2 0 0 'FIBER.CLADDING' 
INTERFACE COATING ABSORB AIR DOPED 
SHIFT X (F2POSX) 
ROTATE Y (ANGLE) (F2POSZ) (F2POSX)-(CLADDINGTAPER)/2 

!! Detector 
PLANE Z (F2POSZ)+(F2LENGTH)+(FIBER) ELLIPSE (CORE)/2 (CORE)/2 'DETECTOR' 
INTERFACE COATING ABSORB DOPED AIR 
SHIFT X (F2POSX) 
ROTATE Y (ANGLE) (F2POSZ) (F2POSX)-(CLADDINGTAPER)/2 

!! Laser-induced fluorescence imitating light source with 100000 rays 
RAYS 0 
IMMERSE WATER             !! light source is inside the water 

EMITTING SPHEROID 0 0 -12.5 25 25 25 100000 'FLUOROPHORE' ISO 
FLUX TOTAL 100 
WAVELENGTHS 600 NANOMETERS 
HALT 10000 

  CONSIDER ALL 
  WINDOW Z X 
  TRACE PLOT OVERLAY 
  PLOT FACETS 
  $VIEW 
  CONSIDER ONLY DETECTOR 
  STATS ALL 