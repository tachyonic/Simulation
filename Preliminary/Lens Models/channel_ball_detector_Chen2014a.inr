CONSIDER ALL 

SYSTEM NEW 
RESET 

!! Define units and wavelength
UNITS MICRONS 'Watts" 
WAVELENGTHS 550 NANOMETERS 

!! Define media
MEDIA ; 1.43 'PDMS' 
MEDIA ; 1.51 'GLASS' 
MEDIA ; 1.33 'WATER' 
MEDIA ; 1.77 'SAPH'
                    
!! Including 
SPLIT 1500 MONTECARLO       !! monte carlo simulation
LEVEL 1000 OFF 
FRESNEL AVE                 !! fresnel equation

!! Simulation Variables
CHIP_LENGTH=2000            !! Length and width of microchip
PDMS_THICK_BOTTOM=300       !! Total thickness of PDMS adjacent/below the channels  
GLASS_THICK=200             !! Total thickness of glass layer

EM_WAVE=0.550               !! emission wavelength

LENS_DIA=100                !! Diamter of the lens  
LENS_PDMS_THICK=20          !! Thickness of PDMS layer above the lens
LENS_F=260 
PDMS_THICK_TOP=(LENS_F)     !! Thickness of PDMS layer above the channels
LENS_PCH=120

!! Coating properties
COATING PROPERTIES 0; 0 1 'TRANSMIT' 
COATING PROPERTIES 0; 0 0 'ABSORB' 
COATING PROPERTIES 0; 1 0 'REFLECT' 

!! Bounding Surface 
SURFACE 
PLANE Z (PDMS_THICK_TOP) 0 0 ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'BoundingPlane' 
PLOT SURFACE 

!! Beginning of geometry modeling 
ENT OBJ 

!! Channel 
PLANE Z 0 RECTANGLE (CHIP_LENGTH)/2.2 12.5 'CHANNEL.TOP' 
INTERFACE COATING BARE WATER PDMS 

PLANE Z -25 RECTANGLE (CHIP_LENGTH)/2.2 12.5 'CHANNEL.B0TTOM' 
INTERFACE COATING BARE WATER PDMS 

PLANE Y 0 RECTANGLE 12.5 (CHIP_LENGTH)/2.2 'CHANNEL.BACK' 
INTERFACE COATING BARE WATER PDMS 
SHIFT Z -12.5 
SHIFT Y 12.5 

PLANE Y 0 RECTANGLE 12.5 (CHIP_LENGTH)/2.2 'CHANNEL.FRONT' 
INTERFACE COATING BARE WATER PDMS 
SHIFT Z -12.5 
SHIFT Y -12.5 

PLANE X 0 RECTANGLE 12.5 12.5 'CHANNEL.RIGHT' 
INTERFACE COATING BARE WATER PDMS 
SHIFT X (CHIP_LENGTH)/2.2 
SHIFT Z -12.5 

PLANE X 0 RECTANGLE 12.5 12.5 'CHANNEL.LEFT' 
INTERFACE COATING BARE WATER PDMS 
SHIFT X -(CHIP_LENGTH)/2.2 
SHIFT Z -12.5 


!! Glass-layer (substrate)
TUBE Z -(PDMS_THICK_BOTTOM)  (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 -(PDMS_THICK_BOTTOM)-(GLASS_THICK) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 0 0 'GLASS.SIDE' 
INTERFACE COATING BARE GLASS AIR 

PLANE Z -(PDMS_THICK_BOTTOM)-(GLASS_THICK) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'GLASS.BOTTOM' 
INTERFACE COATING BARE GLASS AIR  


!! PDMS-layer 
TUBE Z (PDMS_THICK_TOP)+(LENS_DIA)+(LENS_PDMS_THICK) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 -(PDMS_THICK_BOTTOM) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 0 0 'MANTEL.SIDE' 
INTERFACE COATING BARE PDMS AIR 

PLANE Z -(PDMS_THICK_BOTTOM) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'MANTEL.BOTTOM' 
INTERFACE COATING BARE PDMS GLASS 


!! Detector 
PLANE Z (PDMS_THICK_TOP)+(LENS_DIA)+(LENS_PDMS_THICK) ELLIPSE (LENS_DIA)/2 (LENS_DIA)/2 'DETECTOR' 
INTERFACE COATING ABSORB DOPED AIR 

!! Lens
$ITER N -(LENS_PCH) (LENS_PCH) -3
{  
    OPTICAL Z (PDMS_THICK_TOP)+(LENS_DIA)/2 -(LENS_DIA)/2 ELLIPSE (LENS_DIA)/2 'BALL.TOP'  
    INTERFACE COATING TRANSMIT SAPH PDMS 
    SHIFT X (N) 
          
    OPTICAL Z (PDMS_THICK_TOP)-(LENS_DIA)/2 (LENS_DIA)/2 ELLIPSE (LENS_DIA)/2 'BALL.BOTTOM'  
    INTERFACE COATING TRANSMIT SAPH PDMS
    SHIFT X (N)
} 
 

!! Laser-induced fluorescence imitating light source with 100000 rays 
RAYS 0 
IMMERSE WATER             !! light source is inside the water 

EMITTING SPHEROID 0 0 -12.5 10 10 10 100 'FLUOROPHORE' ISO 
FLUX TOTAL 100 
WAVELENGTHS 550 NANOMETERS 
HALT 10000 

  CONSIDER ALL 
  WINDOW Z X 
  TRACE PLOT OVERLAY
  PLOT FACETS 
  $VIEW 
  CONSIDER ONLY DETECTOR 
  STATS ALL  