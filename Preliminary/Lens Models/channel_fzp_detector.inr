CONSIDER ALL 

SYSTEM NEW 
RESET 

!! Define units and wavelength
UNITS MICRONS 'Watts" 
WAVELENGTHS 488 NANOMETERS 

!! Define media
MEDIA ; 1.43 'PDMS' 
MEDIA ; 1.30 'GLASS' 
MEDIA ; 1.33 'WATER' 
MEDIA ; 1.46 'SILICA' 
MEDIA ; 1.44 'DOPED' 
MEDIA ; 1.00 'FZP'

!! Including 
SPLIT 1500 MONTECARLO       !! monte carlo simulation
LEVEL 1000 OFF 
FRESNEL AVE                 !! fresnel equation

!! Simulation Variables
FOCAL_LENGTH=100            !! Thickness of PDMS layer above the lens
PDMS_THICK_TOP=40           !! Thickness of PDMS layer above the channels
CHIP_LENGTH=2000            !! Length and width of microchip
PDMS_THICK_BOTTOM=300       !! Total thickness of PDMS adjacent/below the channels  
GLASS_THICK=200             !! Total thickness of glass layer
EM_WAVE=0.670               !! emission wavelength
LENS_THICK=2*(EM_WAVE)*3.14 !! Thickness of the lens

!! Coating properties
COATING PROPERTIES 0; 0 1 'TRANSMIT' 
COATING PROPERTIES 0; 0 0 'ABSORB' 
COATING PROPERTIES 0; 1 0 'REFLECT' 

!! Bounding Surface 
SURFACE 
PLANE Z (PDMS_THICK_TOP)+(LENS_THICK)+(FOCAL_LENGTH) 0 0 ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'BoundingPlane' 
PLOT SURFACE 

!! Beginning of geometry modeling 
ENT OBJ 

!! Channel 
PLANE Z 0 RECTANGLE (CHIP_LENGTH)/2.2 12.5 'CHANNEL.TOP' 
INTERFACE COATING BARE WATER PDMS 

PLANE Z -25 RECTANGLE (CHIP_LENGTH)/2.2 12.5 'CHANNEL.B0TTOM' 
INTERFACE COATING BARE WATER PDMS 

PLANE Y 0 RECTANGLE 12.5 (CHIP_LENGTH)/2.2 'CHANNEL.BACK' 
INTERFACE COATING BARE WATER PDMS 
SHIFT Z -12.5 
SHIFT Y 12.5 

PLANE Y 0 RECTANGLE 12.5 (CHIP_LENGTH)/2.2 'CHANNEL.FRONT' 
INTERFACE COATING BARE WATER PDMS 
SHIFT Z -12.5 
SHIFT Y -12.5 

PLANE X 0 RECTANGLE 12.5 12.5 'CHANNEL.RIGHT' 
INTERFACE COATING BARE WATER PDMS 
SHIFT X (CHIP_LENGTH)/2.2 
SHIFT Z -12.5 

PLANE X 0 RECTANGLE 12.5 12.5 'CHANNEL.LEFT' 
INTERFACE COATING BARE WATER PDMS 
SHIFT X -(CHIP_LENGTH)/2.2 
SHIFT Z -12.5 


!! Glass-layer (substrate)
TUBE Z -(PDMS_THICK_BOTTOM)  (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 -(PDMS_THICK_BOTTOM)-(GLASS_THICK) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 0 0 'GLASS.SIDE' 
INTERFACE COATING BARE GLASS AIR 

PLANE Z -(PDMS_THICK_BOTTOM)-(GLASS_THICK) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'GLASS.BOTTOM' 
INTERFACE COATING BARE GLASS AIR  


!! PDMS-layer 
TUBE Z (PDMS_THICK_TOP) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 -(PDMS_THICK_BOTTOM) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 0 0 'MANTEL.SIDE' 
INTERFACE COATING BARE PDMS AIR 

PLANE Z -(PDMS_THICK_BOTTOM) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'MANTEL.BOTTOM' 
INTERFACE COATING BARE PDMS GLASS 

!! Coverslip
TUBE Z (PDMS_THICK_TOP)+(LENS_THICK)+(FOCAL_LENGTH) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 (PDMS_THICK_TOP)+(LENS_THICK) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 0 0 'COVERSLIP.SIDE' 
INTERFACE COATING BARE DOPED AIR 

  !!uncomment for no lens
  !!PLANE Z (PDMS_THICK_TOP)+(LENS_THICK) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'COVERSLIP.BOTTOM' 
  !!INTERFACE COATING BARE DOPED PDMS 

!! Detector 
PLANE Z (PDMS_THICK_TOP)+(LENS_THICK)+(FOCAL_LENGTH) ELLIPSE 100 100 'DETECTOR' 
INTERFACE COATING ABSORB DOPED AIR 

!! Lens

!! Side of simulation for lens layer  
TUBE Z (PDMS_THICK_TOP)+(LENS_THICK)/2 (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 (PDMS_THICK_TOP) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 0 0 'LENS.SIDE.OUTER' 
INTERFACE COATING BARE PDMS AIR

FIRST_RING=SQRT(((FOCAL_LENGTH)+0.5*(EM_WAVE))^2-(FOCAL_LENGTH)^2) 

!! Surface between lens center air gap and glass above
PLANE Z (PDMS_THICK_TOP)+(LENS_THICK) ELLIPSE SQRT(((FOCAL_LENGTH)+0.5*(EM_WAVE))^2-(FOCAL_LENGTH)^2) SQRT(((FOCAL_LENGTH)+0.5*(EM_WAVE))^2-(FOCAL_LENGTH)^2) 'LENS.TOP.AIR.INNER'
INTERFACE COATING BARE FZP DOPED  

!! Surface between lens center air gap and pdms below  
PLANE Z (PDMS_THICK_TOP) ELLIPSE (FIRST_RING) (FIRST_RING) 'LENS.BOTTOM.INNER'
INTERFACE COATING BARE FZP PDMS  
                                      
$ITER N 2 15 7
{ 
  RING_INNER=SQRT(((FOCAL_LENGTH)+0.5*(N-1)*(EM_WAVE))^2-(FOCAL_LENGTH)^2)
  RING_OUTER=SQRT(((FOCAL_LENGTH)+0.5*(N)*(EM_WAVE))^2-(FOCAL_LENGTH)^2) 
  
  !! surface between zones in fzp
  SURFACE
    TUBE Z (PDMS_THICK_TOP)+(LENS_THICK)/2 (RING_INNER) (RING_INNER) (PDMS_THICK_TOP) (RING_INNER) (RING_INNER) 0 0 
  OBJECT 'LENS.SIDE.RING'  
    INTERFACE COATING BARE PDMS FZP 
  
  !! surface between air gap in fzp and glass above          
  EDGE
    LINE 0 0 0 ((RING_OUTER)-(RING_INNER)) 0 0
    SHIFT X SQRT(((FOCAL_LENGTH)+0.5*(N-1)*(EM_WAVE))^2-(FOCAL_LENGTH)^2)
    SHIFT Z (PDMS_THICK_TOP)+(LENS_THICK)/2
    SWEEP AXIS 360 0 0 1
  OBJECT 'LENS.TOP.AIR.RING'
    INTERFACE COATING BARE FZP DOPED
    
}
  
 
$ITER N 1 14 7
{ 
  RING_INNER=SQRT(((FOCAL_LENGTH)+0.5*(N-1)*(EM_WAVE))^2-(FOCAL_LENGTH)^2)
  RING_OUTER=SQRT(((FOCAL_LENGTH)+0.5*(N)*(EM_WAVE))^2-(FOCAL_LENGTH)^2)
  
  !! surface between zones in fzp
  SURFACE
    TUBE Z (PDMS_THICK_TOP)+(LENS_THICK)/2 (RING_INNER) (RING_INNER) (PDMS_THICK_TOP) (RING_INNER) (RING_INNER) 0 0 
  OBJECT 'LENS.SIDE.RING'  
    INTERFACE COATING BARE PDMS FZP 
  
  !! surface between pdms in lens and glass above
  EDGE
    LINE 0 0 0 ((RING_OUTER)-(RING_INNER)) 0 0
    SHIFT X SQRT(((FOCAL_LENGTH)+0.5*(N-1)*(EM_WAVE))^2-(FOCAL_LENGTH)^2)
    SHIFT Z (PDMS_THICK_TOP)+(LENS_THICK)/2
    SWEEP AXIS 360 0 0 1
  OBJECT 'LENS.TOP.PDMS.RING'
    INTERFACE COATING BARE PDMS DOPED
    
} 

  !! surface between outer air ring and filler pdms  
  RING_OUTER=SQRT(((FOCAL_LENGTH)+0.5*(14)*(EM_WAVE))^2-(FOCAL_LENGTH)^2)
  SURFACE
    TUBE Z (PDMS_THICK_TOP)+(LENS_THICK)/2 (RING_OUTER) (RING_OUTER) (PDMS_THICK_TOP) (RING_OUTER) (RING_OUTER) 0 0 
  OBJECT 'LENS.SIDE.RING'  
    INTERFACE COATING BARE PDMS FZP 
    
  !! surface between filler pdms around lens and glass above
  EDGE
    LINE 0 0 0 ((CHIP_LENGTH)-(RING_OUTER))/2 0 0
    SHIFT X (RING_OUTER)
    SHIFT Z (PDMS_THICK_TOP)+(LENS_THICK)
    SWEEP AXIS 360 0 0 1
  OBJECT 'LENS.TOP.PDMS.OUTER'
    INTERFACE COATING BARE PDMS DOPED 
 
$ITER N 2 15 7
{ 
  RING_INNER=SQRT(((FOCAL_LENGTH)+0.5*(N-1)*(EM_WAVE))^2-(FOCAL_LENGTH)^2)
  RING_OUTER=SQRT(((FOCAL_LENGTH)+0.5*(N)*(EM_WAVE))^2-(FOCAL_LENGTH)^2)
  
  !! surface between air in lens and mantel below
  EDGE
    LINE 0 0 0 ((RING_OUTER)-(RING_INNER)) 0 0
    SHIFT X SQRT(((FOCAL_LENGTH)+0.5*(N-1)*(EM_WAVE))^2-(FOCAL_LENGTH)^2)
    SHIFT Z (PDMS_THICK_TOP)
    SWEEP AXIS 360 0 0 1
  OBJECT 'LENS.BOTTOM.AIR'
    INTERFACE COATING BARE PDMS AIR 
} 
  
 
  
!! Laser-induced fluorescence imitating light source with 100000 rays 
IMMERSE WATER             !! light source is inside the water 

EMITTING SPHEROID 0 0 -12.5 25 25 25 100 'FLUOROPHORE' ISO 
FLUX TOTAL 100 
WAVELENGTHS 600 NANOMETERS 
HALT 10000 

  CONSIDER ALL 
  WINDOW Z X 
  TRACE PLOT OVERLAY 
  PLOT FACETS 
  $VIEW 
  CONSIDER ONLY DETECTOR 
  STATS ALL 