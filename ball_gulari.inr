  $ECHO NONE
  $IO OUTPUT GEOMETRY\BALL\PAPERS\GULARI.CSV +FILE(10)
  $END
  $SCR 1
CUBIC ZIRCONIA BALL, BARE COATING, VARIED GLASS, constant channel, well diff calculated, iterate over det_rad, lens_dia, n_oil (pdms=1.43, OIL=1.52), SLIDE HEIGHT
  $SCR 1
EMISSION (um), N_WELL , N_OIL, N_LENS, COAT_REFL, LENS_DIA (um), EFF (um), BFL (um), CHAN WIDTH (um), WELL HEIGHT (um), WELL DIFF HEIGHT (um), WELL WIDTH (um), SLIDE_HEIGHT (um), AIR_GAP (um), WD (um), DET_RAD (um), BASE_RAYS, N_RAYS, BASE_RAYS_AREA, N_RAYS_AREA \
  $IO OUTPUT CLOSE
  $ECHO


LOOP_1  {  !! nested macros
                        
$ITER DET_RAD 50 300 -19 NUMBEROFRAYS 
  {  
  &REG LENS_DIA
  CONSIDER ALL
  SYSTEM NEW
  RESET   
  
  !! Optical variables
  FRESNEL AVE
  FLUX_TOTAL=10000
  EM_WAVE=0.590       !! emission wavelength
  SPOT_SIZE=10
      
  !! Define units and wavelength
  UNITS MICRONS 'Watts" 
  WAVELENGTHS (EM_WAVE)*1000 NANOMETERS 

  !! Define media
  N_LENS=2.1
  !!N_OIL=1.52
  N_WELL=1.43
  MEDIA ; 1.43     'PDMS' 
  MEDIA ; 1.44 	   'DOPED'
  MEDIA ; 1.51     'GLASS'
  MEDIA ; 1.33     'WATER'
  MEDIA ; (N_WELL) 'WELL' 
  MEDIA ; (N_OIL)  'OIL' 
  MEDIA ; (N_LENS) 'BALL'
  N_EFF=(N_LENS)/(N_OIL)
                      
  !! Including 
  SPLIT 1500 MONTECARLO       !! monte carlo simulation
  LEVEL 1000 OFF 
  FRESNEL AVE                 !! fresnel equation

  !! Coating properties
  COAT_REFL=-1!!R^2
  COAT_TRANS=1-(COAT_REFL)^2
  COATING PROPERTIES 0; 0 1 'TRANSMIT' 
  COATING PROPERTIES 0; 0 0 'ABSORB' 
  COATING PROPERTIES 0; 1 0 'REFLECT' 
  COATING PROPERTIES 0; (COAT_REFL) (COAT_TRANS) 'REFL_COAT' 
    
  !! Simulation Variables
  
  !! Chip variables
  CHIP_LENGTH=2000            !! Length and width of microchip
  PDMS_THICK_BOTTOM=300       !! Total thickness of PDMS adjacent/below the channels  
  GLASS_THICK=200             !! Total thickness of glass layer
  CHAN_WIDTH=25
  !!SLIDE_HEIGHT=170
  
  !! Lens dimensions
  !!LENS_DIA=50		          !! Diameter of the lens
  LENS_RAD=(LENS_DIA)/2  
  
      TERM_A=(N_EFF)-1
      TERM_B=2/(LENS_RAD)
      TERM_C=((TERM_A)*(LENS_DIA))/((N_EFF)*(LENS_RAD)*(LENS_RAD))
      LENS_P=(TERM_A)*((TERM_B)-(TERM_C))

  EFL=1/(LENS_P)
  BFL=(EFL)-(LENS_RAD)  
  
  !! microwell variables
  
  $IF (BFL) LT (SLIDE_HEIGHT)+10 THEN
      WELL_DIFF=0
  $ELSE
      WELL_DIFF=(BFL)-(SLIDE_HEIGHT)-10
  $ENDIF
  
  WELL_WIDTH=(LENS_DIA)
  WELL_HEIGHT=(LENS_RAD)+(WELL_DIFF)
  
  WD=(SLIDE_HEIGHT)+(WELL_HEIGHT)+(LENS_RAD)		!! WORKING DISTANCE BEFORE AIR GAP
  AIR_GAP=2                                         !! air gap beneath detector
  !!DET_RAD=50
   
  !! Bounding Surface 
  SURFACE 
  PLANE Z (WD)+(AIR_GAP) 0 0 ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'BoundingPlane' 
  !!PLOT SURFACE 

  !! Beginning of geometry modelling 
  ENT OBJ 
  
  !! Channel   
  PLANE Z 0 RECTANGLE (((CHIP_LENGTH)/2.2)-(WELL_WIDTH))/2 (CHAN_WIDTH)/2 'CHANNEL.TOP' 
  INTERFACE COATING BARE WATER GLASS
  
  PLANE Z -(CHAN_WIDTH) RECTANGLE (CHIP_LENGTH)/2.2 (CHAN_WIDTH)/2 'CHANNEL.BOTTOM' 
  INTERFACE COATING BARE WATER PDMS 
  
  PLANE Y 0 RECTANGLE (CHAN_WIDTH)/2 (CHIP_LENGTH)/2.2 'CHANNEL.BACK' 
  INTERFACE COATING TRANSMIT WATER PDMS 
  SHIFT Z -(CHAN_WIDTH)/2 
  SHIFT Y (CHAN_WIDTH)/2 
  
  PLANE Y 0 RECTANGLE (CHAN_WIDTH)/2 (CHIP_LENGTH)/2.2 'CHANNEL.FRONT' 
  INTERFACE COATING TRANSMIT WATER PDMS 
  SHIFT Z -(CHAN_WIDTH)/2 
  SHIFT Y -(CHAN_WIDTH)/2 
  
  PLANE X 0 RECTANGLE (CHAN_WIDTH)/2 (CHAN_WIDTH)/2 'CHANNEL.RIGHT' 
  INTERFACE COATING BARE WATER PDMS 
  SHIFT X (CHIP_LENGTH)/2.2 
  SHIFT Z -(CHAN_WIDTH)/2 
  
  PLANE X 0 RECTANGLE (CHAN_WIDTH)/2 (CHAN_WIDTH)/2 'CHANNEL.LEFT' 
  INTERFACE COATING BARE WATER PDMS 
  SHIFT X -(CHIP_LENGTH)/2.2 
  SHIFT Z -(CHAN_WIDTH)/2  
 
  !! Glass-layer (substrate)
  TUBE Z -(PDMS_THICK_BOTTOM)  (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 -(PDMS_THICK_BOTTOM)-(GLASS_THICK) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 0 0 'GLASS.SIDE' 
  INTERFACE COATING BARE GLASS AIR 
          
  PLANE Z -(PDMS_THICK_BOTTOM)-(GLASS_THICK) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'GLASS.BOTTOM' 
  INTERFACE COATING BARE GLASS AIR  
  
  !! PDMS-layer
  TUBE Z 0 (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 -(PDMS_THICK_BOTTOM) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 0 0 'MANTEL.SIDE' 
  INTERFACE COATING BARE PDMS AIR 
  
  PLANE Z -(PDMS_THICK_BOTTOM) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'MANTEL.BOTTOM' 
  INTERFACE COATING BARE PDMS GLASS 

  !! Slide-layer
  TUBE Z (SLIDE_HEIGHT) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 0 (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 0 0 'SLIDE.SIDE' 
  INTERFACE COATING BARE GLASS AIR 
  
  PLANE Z 0 ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'SLIDE.BOTTOM' 
  INTERFACE COATING BARE GLASS PDMS  !! TO DO: turn to two semicircles for channels?
  
  !! PDMS-layer
  TUBE Z (WD) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 (SLIDE_HEIGHT) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 0 0 'WELL.TUBE.SIDE' 
  INTERFACE COATING BARE WELL AIR 
  
  PLANE Z (SLIDE_HEIGHT) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'WELL.TUBE.BOTTOM' 
  INTERFACE COATING BARE WELL GLASS   !! TO DO: turn to 4 segments for well?
  
  !! Air Gap
  PLANE Z (WD) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'AIR_GAP' 
  INTERFACE COATING BARE PDMS AIR 
  
  !! Detector 
  PLANE Z (WD)+(AIR_GAP) ELLIPSE (DET_RAD) (DET_RAD) 'DETECTOR' 
  INTERFACE COATING ABSORB DOPED AIR     
 
 !! Laser-induced fluorescence imitating light source with 100000 rays 
  RAYS 0 
  IMMERSE WATER             !! light source is inside the water 
  
  EMITTING SPHEROID 0 0 -(CHAN_WIDTH)/2 (SPOT_SIZE) (SPOT_SIZE) (SPOT_SIZE) (FLUX_TOTAL) 'FLUOROPHORE' ISO 
  FLUX TOTAL (FLUX_TOTAL) 
  WAVELENGTHS (EM_WAVE)*1000 NANOMETERS 
  HALT 10000 
  
    CONSIDER ALL 
    WINDOW Z X 
    TRACE PLOT OVERLAY
    PLOT FACETS 
    !!$VIEW 
    CONSIDER ONLY DETECTOR 
    STATS ALL        
    
    $GRAB 'TOTAL' 0 1 BASENUMBEROFRAYS
    &REG 

  BASERAYS_AREA=(BASENUMBEROFRAYS)/(3.14159*(DET_RAD)^2)
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
   
  ENT OBJ 
  
  !! Well
  
  PLANE Y 0 RECTANGLE (WELL_HEIGHT)/2 (WELL_WIDTH)/2 'WELL.BACK' 
  INTERFACE COATING BARE OIL PDMS 
  SHIFT Z (SLIDE_HEIGHT)+(WELL_HEIGHT)/2
  SHIFT Y (WELL_WIDTH)/2
  
  PLANE Y 0 RECTANGLE (WELL_HEIGHT)/2 (WELL_WIDTH)/2 'WELL.FRONT' 
  INTERFACE COATING BARE OIL PDMS 
  SHIFT Z (SLIDE_HEIGHT)+(WELL_HEIGHT)/2  
  SHIFT Y -(WELL_WIDTH)/2

  PLANE X 0 RECTANGLE (WELL_WIDTH)/2 (WELL_HEIGHT)/2 'WELL.LEFT' 
  INTERFACE COATING BARE OIL PDMS              
  SHIFT X -(WELL_WIDTH)/2 
  SHIFT Z (SLIDE_HEIGHT)+(WELL_HEIGHT)/2
  
  PLANE X 0 RECTANGLE (WELL_WIDTH)/2 (WELL_HEIGHT)/2 'WELL.RIGHT' 
  INTERFACE COATING BARE OIL PDMS              
  SHIFT X (WELL_WIDTH)/2 
  SHIFT Z (SLIDE_HEIGHT)+(WELL_HEIGHT)/2
  
  PLANE Z (SLIDE_HEIGHT) RECTANGLE (WELL_WIDTH)/2 (WELL_WIDTH)/2 'WELL.BOTTOM' 
  INTERFACE COATING BARE OIL GLASS  

  !! Lens  
    OPTICAL Z (SLIDE_HEIGHT)+(WELL_DIFF)+(LENS_DIA) -(LENS_RAD) ELLIPSE (LENS_RAD) 'BALL.TOP'  
    INTERFACE COATING TRANSMIT BALL PDMS 
       
    OPTICAL Z (SLIDE_HEIGHT)+(WELL_DIFF) (LENS_RAD) ELLIPSE (LENS_RAD) 'BALL.BOTTOM'  
    INTERFACE COATING TRANSMIT BALL OIL
    
  !! Laser-induced fluorescence imitating light source with 100000 rays 
  RAYS 0 
  IMMERSE WATER             !! light source is inside the water 
  
  EMITTING SPHEROID 0 0 -(CHAN_WIDTH)/2 (SPOT_SIZE) (SPOT_SIZE) (SPOT_SIZE) (FLUX_TOTAL) 'FLUOROPHORE' ISO 
  FLUX TOTAL (FLUX_TOTAL) 
  WAVELENGTHS (EM_WAVE)*1000 NANOMETERS 
  HALT 10000 
  
    CONSIDER ALL 
    WINDOW Z X 
    TRACE PLOT OVERLAY
    PLOT FACETS 
    !!$VIEW 
    CONSIDER ONLY DETECTOR 
    STATS ALL        
    
    $GRAB 'TOTAL' 0 1 NUMBEROFRAYS
    &REG 

  RAYS_AREA=(NUMBEROFRAYS)/(3.14159*(DET_RAD)^2)
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    
  !!WRITES TO TEXT FILE ON EACH PASS
  $ECHO NONE                                          
  $IO OUTPUT GEOMETRY\BALL\PAPERS\GULARI.CSV +FILE(10)   !! ALL SUCCESSIVE ITERATIONS
  $END
  $SCR 1
\EM_WAVE.\, \N_WELL.\, \N_OIL.\, \N_LENS.\, \COAT_REFL.\, \LENS_DIA.\, \EFL.\, \BFL.\, \CHAN_WIDTH.\, \WELL_HEIGHT.\ ,\WELL_DIFF.\ ,\WELL_WIDTH.\, \SLIDE_HEIGHT.\, \AIR_GAP.\, \WD.\, \DET_RAD.\, \BASENUMBEROFRAYS.\, \NUMBEROFRAYS.\, \BASERAYS_AREA.\, \RAYS_AREA.\
  $IO OUTPUT CLOSE
  $ECHO
  
  }   
  
}
  

LOOP_2  {
    $ITER LENS_DIA 120 600 -13  
    {
      &REG LENS_DIA
      $LOOP_1 ?
    }
}

LOOP_3  {
    $ITER N_OIL 1.33 1.53 -3  
    {
      &REG N_OIL
      $LOOP_2 ?
    }
}  

!!LOOP_4  {
!!    $ITER COAT_REFL 0 1 -5  
!!    {
!!      &REG COAT_REFL
!!      $LOOP_3 ?
!!    }
!!}


!!LOOP_5  {
    $ITER SLIDE_HEIGHT 50 100 -3  
    {
      &REG SLIDE_HEIGHT
      $LOOP_3 ?
    }
!!}


!!$ITER N_LENS 1.768 2.1 -2
!!{  
!!  &REG N_LENS  
!!  $LOOP_4 ?
!!}


                            
RETURN

