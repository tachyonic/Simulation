  $ECHO NONE
  $IO OUTPUT GEOMETRY\BALL\COMMERCIAL\SIMPLE.CSV +FILE(10)
  $END
  $SCR 1
150um sapphire ball - testing calculation of NA_simulated
  $SCR 1
EMISSION (um), N_LENS, LENS_DIA (um), EFF (um), BFL (um), PDMS_CHAN_LENS (um), PDMS_LENS_DET (um), COAT_REFL , AIR_GAP (um), WD_PDMS, DET_RAD (um), BASE_RAYS, N_RAYS, BASETHETA_CALC, BASETHETA_RAYS, LENSTHETA_RAYS

  $IO OUTPUT CLOSE
  $ECHO

  CONSIDER ALL
  SYSTEM NEW
  RESET   
  
  !! Optical variables
  FRESNEL AVE
  FLUX_TOTAL=10000
  EM_WAVE=0.590       !! emission wavelength
  SPOT_SIZE=10
      
  !! Define units and wavelength
  UNITS MICRONS 'Watts" 
  WAVELENGTHS (EM_WAVE)*1000 NANOMETERS 

  !! Define media
  N_LENS=1.768
  MEDIA ; 1.43     'PDMS' 
  MEDIA ; 1.44     'DOPED' 
  MEDIA ; 1.51     'GLASS'
  MEDIA ; 1.33     'WATER'
  MEDIA ; (N_LENS) 'BALL'
  N_EFF=(N_LENS)/1.43
                      
  !! Including 
  SPLIT 1500 MONTECARLO       !! monte carlo simulation
  LEVEL 1000 OFF 
  FRESNEL AVE                 !! fresnel equation

  !! Coating properties
  COATING PROPERTIES 0; 0 1 'TRANSMIT' 
  COATING PROPERTIES 0; 0 0 'ABSORB' 
  COATING PROPERTIES 0; 1 0 'REFLECT' 
    
  !! Simulation Variables
  
  !! Chip variables
  CHIP_LENGTH=2000            !! Length and width of microchip
  PDMS_THICK_BOTTOM=300       !! Total thickness of PDMS adjacent/below the channels  
  GLASS_THICK=200             !! Total thickness of glass layer
  CHAN_WIDTH=25
  
  !! Lens dimensions
  LENS_DIA=150                 !! Diamter of the lens
  LENS_RAD=(LENS_DIA)/2  
  
      TERM_A=(N_EFF)-1
      TERM_B=2/(LENS_RAD)
      TERM_C=((TERM_A)*(LENS_DIA))/((N_EFF)*(LENS_RAD)*(LENS_RAD))
      LENS_P=(TERM_A)*((TERM_B)-(TERM_C))

  EFL=1/(LENS_P)
  BFL=(EFL)-(LENS_RAD)
  
  PDMS_CHAN_LENS=(BFL)  !! Thickness of PDMS layer above the channel
  PDMS_LENS_DET=170
  
  WD_PDMS=(PDMS_LENS_DET)+(PDMS_CHAN_LENS)+(LENS_DIA)
  
  AIR_GAP=2                   !! air gap beneath detector
  DET_RAD=50
   
  !! Bounding Surface 
  SURFACE 
  PLANE Z (WD_PDMS)+(AIR_GAP) 0 0 ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'BoundingPlane' 
  !!PLOT SURFACE 

  !! Beginning of geometry modeling 
  ENT OBJ 
  
  !! Channel 
  PLANE Z 0 RECTANGLE (CHIP_LENGTH)/2.2 (CHAN_WIDTH)/2 'CHANNEL.TOP' 
  INTERFACE COATING BARE WATER PDMS 
  
  PLANE Z -(CHAN_WIDTH) RECTANGLE (CHIP_LENGTH)/2.2 (CHAN_WIDTH)/2 'CHANNEL.B0TTOM' 
  INTERFACE COATING BARE WATER PDMS 
  
  PLANE Y 0 RECTANGLE (CHAN_WIDTH)/2 (CHIP_LENGTH)/2.2 'CHANNEL.BACK' 
  INTERFACE COATING BARE WATER PDMS 
  SHIFT Z -(CHAN_WIDTH)/2 
  SHIFT Y (CHAN_WIDTH)/2 
  
  PLANE Y 0 RECTANGLE (CHAN_WIDTH)/2 (CHIP_LENGTH)/2.2 'CHANNEL.FRONT' 
  INTERFACE COATING BARE WATER PDMS 
  SHIFT Z -(CHAN_WIDTH)/2 
  SHIFT Y -(CHAN_WIDTH)/2 
  
  PLANE X 0 RECTANGLE (CHAN_WIDTH)/2 (CHAN_WIDTH)/2 'CHANNEL.RIGHT' 
  INTERFACE COATING BARE WATER PDMS 
  SHIFT X (CHIP_LENGTH)/2.2 
  SHIFT Z -(CHAN_WIDTH)/2 
  
  PLANE X 0 RECTANGLE (CHAN_WIDTH)/2 (CHAN_WIDTH)/2 'CHANNEL.LEFT' 
  INTERFACE COATING BARE WATER PDMS 
  SHIFT X -(CHIP_LENGTH)/2.2 
 
  !! Glass-layer (substrate)
  TUBE Z -(PDMS_THICK_BOTTOM)  (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 -(PDMS_THICK_BOTTOM)-(GLASS_THICK) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 0 0 'GLASS.SIDE' 
  INTERFACE COATING BARE GLASS AIR 
          
  PLANE Z -(PDMS_THICK_BOTTOM)-(GLASS_THICK) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'GLASS.BOTTOM' 
  INTERFACE COATING BARE GLASS AIR  
  
  !! PDMS-layer
  TUBE Z (WD_PDMS) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 -(PDMS_THICK_BOTTOM) (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 0 0 'MANTEL.SIDE' 
  INTERFACE COATING BARE PDMS AIR 
  
  PLANE Z -(PDMS_THICK_BOTTOM) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'MANTEL.BOTTOM' 
  INTERFACE COATING BARE PDMS GLASS 
  
  !! Air Gap
  PLANE Z (WD_PDMS) ELLIPSE (CHIP_LENGTH)/2 (CHIP_LENGTH)/2 'AIR_GAP' 
  INTERFACE COATING BARE PDMS AIR 
  
  !! Detector 
  PLANE Z (WD_PDMS)+(AIR_GAP) ELLIPSE (DET_RAD) (DET_RAD) 'DETECTOR' 
  INTERFACE COATING ABSORB DOPED AIR     
  
  !!!!!!!!!!!!!!!!!!!
  RAYS 0 
  IMMERSE WATER             !! light source is inside the water 
      
  EMITTING SPHEROID 0 0 -(CHAN_WIDTH)/2 (SPOT_SIZE) (SPOT_SIZE) (SPOT_SIZE) (FLUX_TOTAL) 'FLUOROPHORE' ISO 
  FLUX TOTAL (FLUX_TOTAL) 
  WAVELENGTHS (EM_WAVE)*1000 NANOMETERS 
  HALT 10000 
  
    CONSIDER ALL 
    WINDOW Z X 
    TRACE PLOT OVERLAY
    PLOT FACETS
    CONSIDER ONLY DETECTOR 
    STATS ALL        
    
    $GRAB 'TOTAL' 0 1 BASENUMBEROFRAYS
    
	  !!EXTREMES DIRECTION
	  !!$GRAB 'Ray with MINIMUM A component' 1 1 MNAX MNAY MNAZ
	
  BASERAYS_AREA=(BASENUMBEROFRAYS)/(3.14159*(DET_RAD)^2) 
  BASETHETA_CALC=ATAN[(DET_RAD)/((WD_PDMS)+(AIR_GAP))]
  !!BASETHETA_SIM=ACOS[(MNAZ)/(SQRT((MNAX)^2+(MNAY)^2+(MNAZ)^2))]
  BASETHETA_RAYS=ACOS[1-(2*(BASENUMBEROFRAYS)/(FLUX_TOTAL))]
  
  !!!!!!!!!!!!!!!!!!!
  
  !! Lens
  ENT OBJ
  
    OPTICAL Z (PDMS_CHAN_LENS)+(LENS_DIA) -(LENS_RAD) ELLIPSE (LENS_RAD) 'BALL.TOP'  
    INTERFACE COATING TRANSMIT BALL PDMS 
       
    OPTICAL Z (PDMS_CHAN_LENS) (LENS_RAD) ELLIPSE (LENS_RAD) 'BALL.BOTTOM'  
    INTERFACE COATING TRANSMIT BALL PDMS
    
  !! Laser-induced fluorescence imitating light source with 100000 rays 
  RAYS 0 
  IMMERSE WATER             !! light source is inside the water 
  
  EMITTING SPHEROID 0 0 -(CHAN_WIDTH)/2 (SPOT_SIZE) (SPOT_SIZE) (SPOT_SIZE) (FLUX_TOTAL) 'FLUOROPHORE' ISO 
  FLUX TOTAL (FLUX_TOTAL) 
  WAVELENGTHS (EM_WAVE)*1000 NANOMETERS 
  HALT 10000 
  
    CONSIDER ALL 
    WINDOW Z X 
    TRACE PLOT OVERLAY
    PLOT FACETS 
    !!$VIEW 
    CONSIDER ONLY DETECTOR 
    STATS ALL
    
    $GRAB 'TOTAL' 0 1 NUMBEROFRAYS
            
    !!CONSIDER ONLY BALL 
	  !!EXTREMES DIRECTION
	  !!$GRAB 'Ray with MINIMUM A component' 1 1 MNAX MNAY MNAZ
	
  !!!!!!!!!!!!!!!!!!!

  !!RAYS_AREA=(NUMBEROFRAYS)/(3.14159*(DET_RAD)^2)                            
  !!LENSTHETA_CALC=ATAN[(LENS_DIA)/((PDMS_CHAN_LENS)+(LENS_RAD))]     !! based on assumption that everything through ball reaches detector
  !!LENSTHETA_SIM=ACOS[(MNAZ)/(SQRT((MNAX)^2+(MNAY)^2+(MNAZ)^2))]
  LENSTHETA_RAYS=ACOS[1-(2*(NUMBEROFRAYS)/(FLUX_TOTAL))]
  
  !!WRITES TO TEXT FILE ON EACH PASS
  $ECHO NONE
  $IO OUTPUT GEOMETRY\BALL\COMMERCIAL\SIMPLE.CSV +FILE(10)   !! ALL SUCCESSIVE ITERATIONS
  $END
  $SCR 1
\EM_WAVE.\, \N_LENS.\ , \LENS_DIA.\, \EFL.\ , \BFL.\ ,\PDMS_CHAN_LENS.\ , \PDMS_LENS_DET.\ , \COAT_REFL.\, \AIR_GAP.\, \WD_PDMS.\, \DET_RAD.\, \BASENUMBEROFRAYS.\ , \NUMBEROFRAYS.\, \BASETHETA_CALC.\, \BASETHETA_RAYS.\, \LENSTHETA_RAYS.\

  $IO OUTPUT CLOSE
  $ECHO

                            
RETURN

